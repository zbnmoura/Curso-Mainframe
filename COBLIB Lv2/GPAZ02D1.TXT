      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         GPAZ02D1.
       AUTHOR.                             BRUNO NEVES DE MOURA
       DATE-WRITTEN.                       28/04/2016.
      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
      *----------------------------------------------------------------*
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       INPUT-OUTPUT                        SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.
           SELECT CADPRDD1 ASSIGN          TO UT-S-CADPRDD1
           FILE STATUS IS FS-CADPRDD1
           .
      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       FILE                                SECTION.
      *----------------------------------------------------------------*
       FD  CADPRDD1
           RECORDING MODE IS F
           .
       COPY BK001D1 REPLACING ==::== BY ====
       .
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
      *INCLUDE DA BOOKLIB
      *----------------------------------------------------------------*
           EXEC SQL
             INCLUDE BK002TP
           END-EXEC
           EXEC SQL
             INCLUDE SQLCA
           END-EXEC
      *----------------------------------------------------------------*
       COPY BK001D1 REPLACING ==::== BY ==WS-==
       .
       COPY VARDATA
       .
       COPY VARTEMPO
       .
      *-- CONTADORES
       01  WS-CONTADORES-COMP.
           05 WS-CTLIDO                    PIC 9(005) COMP.
           05 WS-CTINS                     PIC 9(005) COMP.
           05 WS-CTDUP                     PIC 9(005) COMP.
           05 WS-CTINV                     PIC 9(005) COMP.
      *-- CONTADORES FORMATADOS
       01  WS-CONTADORES-F.
           05 WS-CTLIDO-F                  PIC ZZ.ZZ9.
           05 WS-CTINS-F                   PIC ZZ.ZZ9.
           05 WS-CTDUP-F                   PIC ZZ.ZZ9.
           05 WS-CTINV-F                   PIC ZZ.ZZ9.
      *-- FILE STATUS
       01  FS-CADPRDD1                     PIC X(002).
           88 SUCESSO-CAD                  VALUE "00".
           88 FIM-ARQ-CAD                  VALUE "10".
      *--
       77  WS-SQLCODE                      PIC +9(09).
       77  WS-MSG                          PIC X(080).
       77  WS-FS                           PIC X(002).
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
               UNTIL FIM-ARQ-CAD
           PERFORM 3000-TERMINO
           STOP RUN
           .
      *----------------------------------------------------------------*
       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL FROM TIME
           MOVE 0                          TO WS-CTLIDO
                                              WS-CTINS
                                              WS-CTDUP
                                              WS-CTINV
           OPEN INPUT CADPRDD1
           IF NOT SUCESSO-CAD
              MOVE "ERRO NA ABERTURA DE CADPRDD1. "
                                           TO WS-MSG
              MOVE FS-CADPRDD1             TO WS-FS
              GO TO 9000-ERRO
           END-IF
           PERFORM 1100-LER-CADPRDD1
           .
      *----------------------------------------------------------------*
       1100-LER-CADPRDD1.
           READ CADPRDD1 INTO WS-REG-CADPRDD1
           IF SUCESSO-CAD
              ADD 1 TO WS-CTLIDO
           ELSE
              IF NOT FIM-ARQ-CAD
                 MOVE "ERRO NA LEITURA DE CADPRDD1. "
                                           TO WS-MSG
                 MOVE FS-CADPRDD1          TO WS-FS
                 GO TO 9000-ERRO
              END-IF
           END-IF
           .
      *----------------------------------------------------------------*
       2000-PROCESSAR.
      *-- MOVE DA ESPELHO PARA A HOSPEDEIRA
           MOVE WS-CODPROD-D1              TO CODPROD
           MOVE +20                        TO DESCPROD-LEN
           MOVE WS-DESCPROD-D1             TO DESCPROD-TEXT
           MOVE WS-UNIDPROD-D1             TO UNIDPROD
           MOVE WS-LOCALPROD-D1            TO LOCALPROD
           MOVE WS-QTDEST-D1               TO QTDEST
           MOVE WS-QTDMAX-D1               TO QTDMAX
           MOVE WS-QTDMIN-D1               TO QTDMIN
           MOVE WS-PRECOCOMPRA-D1          TO PRECOCOMPRA
           MOVE WS-PRECOVENDA-D1           TO PRECOVENDA
           MOVE WS-PERCOMIS-D1             TO PERCOMIS
      *-- INSERINDO OS DADOS DA HOSPEDEIRA NA TABELA
           EXEC SQL
              INSERT INTO TBPRODUTO
              (
                 CODPROD
               , DESCPROD
               , UNIDPROD
               , LOCALPROD
               , QTDEST
               , QTDMAX
               , QTDMIN
               , PRECOCOMPRA
               , PRECOVENDA
               , PERCOMIS
              )
              VALUES
              (
                 :CODPROD
               , :DESCPROD
               , :UNIDPROD
               , :LOCALPROD
               , :QTDEST
               , :QTDMAX
               , :QTDMIN
               , :PRECOCOMPRA
               , :PRECOVENDA
               , : PERCOMIS
              )
           END-EXEC
      *-- TESTANDO SQLCODE
           EVALUATE SQLCODE
              WHEN 0
                 ADD 1 TO WS-CTINS
              WHEN -803
                 ADD 1 TO WS-CTDUP
              WHEN -545
                 ADD 1 TO WS-CTINV
              WHEN OTHER
                 MOVE "ERRO NO SQLCODE. "
                                           TO WS-MSG
                 MOVE SQLCODE              TO WS-SQLCODE
                 GO TO 9000-ERRO-DB2
           END-EVALUATE
           PERFORM 1100-LER-CADPRDD1
           .
      *----------------------------------------------------------------*
       3000-TERMINO.
           CLOSE CADPRDD1
           IF NOT SUCESSO-CAD
              MOVE "ERRO NO FECHAMENTO DE CADPRDD1. "
                                           TO WS-MSG
              MOVE FS-CADPRDD1             TO WS-FS
              GO TO 9000-ERRO
           END-IF
      *
           ACCEPT WS-HORARIO-FINAL FROM TIME
           PERFORM 9000-CALCULA-TEMPO-PROC
      *
           MOVE WS-CTLIDO                  TO WS-CTLIDO-F
           MOVE WS-CTINS                   TO WS-CTINS-F
           MOVE WS-CTDUP                   TO WS-CTDUP-F
           MOVE WS-CTINV                   TO WS-CTINV-F
      *
           PERFORM 9000-IMPRIME-DATA
      *
           DISPLAY "=================================================="
           DISPLAY "==      ESTATISTICA FINAL DE PROCESSAMENTO      =="
           DISPLAY "=================================================="
           DISPLAY "QTDE. PRODUTOS LIDOS CADPRDD1........:" WS-CTLIDO-F
           DISPLAY "QTDE. PRODUTOS INSERIDOS TBPRODUTO...:" WS-CTINS-F
           DISPLAY "QTDE. PRODUTOS DUPLICAODS............:" WS-CTDUP-F
           DISPLAY "QTDE. PRODUTOS INVALIDOS.............:" WS-CTINV-F
           DISPLAY "=================================================="
           DISPLAY "TEMPO TOTAL DE PROCESSAMENTO.........: "
                    WS-TEMPO-PROCESSAMENTO-F
           DISPLAY "=================================================="
           DISPLAY "==     TERMINO NORMAL DO PROGRAMA GPXXNND1      =="
           DISPLAY "=================================================="
           .
      *----------------------------------------------------------------*
      *-- COPYS DE ERRO E DATA
      *----------------------------------------------------------------*
           COPY CALCTEMP
           .
           COPY ROTERRO
           .
           COPY ROTERRO2
           .
           COPY ROTDATA
           .

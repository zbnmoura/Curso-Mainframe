      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         GPGO0204.
       AUTHOR.                             BRUNO NEVES DE MOURA
       DATE-WRITTEN.                       02/02/2016.
       DATE-COMPILED.                      00/02/2016.
       SECURITY.                           NENHUMA.
      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
      *----------------------------------------------------------------*
       SOURCE-COMPUTER.                    L01L05.
       OBJECT-COMPUTER.                    L01L01.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *----------------------------------------------------------------*
       INPUT-OUTPUT                        SECTION.
      *----------------------------------------------------------------*
       FILE-CONTROL.
           SELECT CLISP ASSIGN TO UT-S-CLISP
           FILE STATUS IS FS-CLISP
           .
           SELECT CLIRJ ASSIGN TO UT-S-CLIRJ
           FILE STATUS IS FS-CLIRJ
           .
           SELECT CLIMG ASSIGN TO UT-S-CLIMG
           FILE STATUS IS FS-CLIMG
           .
           SELECT CLIUNIF ASSIGN TO UT-S-CLIUNIF
           FILE STATUS IS FS-CLIUNIF
           .
      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       FILE                                SECTION.
      *----------------------------------------------------------------*
       FD  CLISP
           LABEL RECORDS IS STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
      *    RECORD CONTAINS NN CHARACTERS
           DATA RECORD IS REG-CLISP
           .
           COPY CLISP REPLACING ==::== BY ====
           .
       FD  CLIRJ
           LABEL RECORDS IS STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
      *    RECORD CONTAINS NN CHARACTERS
           DATA RECORD IS REG-CLIRJ
           .
           COPY CLIRJ REPLACING ==::== BY ====
           .
       FD  CLIMG
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
      *    RECORD CONSTAINS NN CHARACTERS
           DATA RECORD IS REG-CLIMG
           .
           COPY CLIMG REPLACING ==::== BY ====
           .
       FD  CLIUNIF
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 0 RECORDS
           RECORDING MODE IS F
      *    RECORD CONTAINS NN CHARACTERS
           DATA RECORD IS REG-CLIUNIF
           .
           COPY CLIUNIF REPLACING ==::== BY ====
           .
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
      *==  VARIAVEL ESPELHO DO ARQ. CLISP                            ==*
           COPY CLISP   REPLACING ==::== BY ==WS-==
           .
      *==  VARIAVEL ESPELHO DO ARQ. CLIRJ                            ==*
           COPY CLIRJ   REPLACING ==::== BY ==WS-==
           .
      *==  VARIAVEL ESPELHO DO ARQ. CLIMG                            ==*
           COPY CLIMG   REPLACING ==::== BY ==WS-==
           .
      *==  VARIAVEL ESPELHO DO ARQ. CLIUNIF                          ==*
           COPY CLIUNIF REPLACING ==::== BY ==WS-==
           .
      *==  VARIAVEL DE DATA                                          ==*
           COPY VARDATA
           .
      *==  VARIAVEL DE TEMPO                                         ==*
           COPY VARTEMPO
           .
      *==  VARIAVEIS DE FILE STATUS                                  ==*
       77  FS-CLISP                        PIC X(002).
       77  FS-CLIRJ                        PIC X(002).
       77  FS-CLIMG                        PIC X(002).
       77  FS-CLIUNIF                      PIC X(002).
      *==  VARIAVEIS CONTADORAS                                      ==*
       77  WS-CTLIDO-SP                    PIC 9(004) COMP.
       77  WS-CTLIDO-RJ                    PIC 9(004) COMP.
       77  WS-CTLIDO-MG                    PIC 9(004) COMP.
       77  WS-CTGRAV-UNIF                  PIC 9(004) COMP.
      *== VARIAVEIS CONTADORAS FORMATADAS                            ==*
       77  WS-CTLIDO-SP-F                  PIC Z(003)9.
       77  WS-CTLIDO-RJ-F                  PIC Z(003)9.
       77  WS-CTLIDO-MG-F                  PIC Z(003)9.
       77  WS-CTGRAV-UNIF-F                PIC Z(003)9.
      *==  VARIAVEIS AUXILIARES                                      ==*
       77  WS-FS                           PIC X(002).
       77  WS-MSG                          PIC X(045).
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
      *== SECAO INUTILIZADA                                          ==*
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
       0000-GPGO0204.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
                   UNTIL FS-CLISP EQUAL "10"
                   AND   FS-CLIRJ EQUAL "10"
                   AND   FS-CLIMG EQUAL "10"
           PERFORM 3000-TERMINO
           STOP RUN
           .
       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL FROM TIME
           MOVE 0                          TO WS-CTLIDO-SP
                                              WS-CTLIDO-RJ
                                              WS-CTLIDO-MG
                                              WS-CTGRAV-UNIF
      *==  ABRINDO ARQUVIO INPUTS E CONDICIONANDO EM ROTINAS DE ERRO ==*
           OPEN INPUT CLISP
           IF FS-CLISP NOT EQUAL "00"
              MOVE "ERRO ABERTURA CLISP"   TO WS-MSG
              MOVE FS-CLISP                TO WS-FS
              PERFORM 9999-ERRO
           END-IF
           OPEN INPUT CLIRJ
           IF FS-CLIRJ NOT EQUAL "00"
              MOVE "ERRO ABERTURA CLIRJ"   TO WS-MSG
              MOVE FS-CLIRJ                TO WS-FS
              PERFORM 9999-ERRO
           END-IF
           OPEN INPUT CLIMG
           IF FS-CLIMG NOT EQUAL "00"
              MOVE "ERRO ABERTURA CLIMG"   TO WS-MSG
              MOVE FS-CLIMG                TO WS-FS
              PERFORM 9999-ERRO
           END-IF
           OPEN OUTPUT CLIUNIF
           IF FS-CLIUNIF NOT EQUAL "00"
              MOVE "ERRO ABERTURA CLIUNIF" TO WS-MSG
              MOVE FS-CLIUNIF              TO WS-FS
              PERFORM 9999-ERRO
           END-IF
           PERFORM 1100-LER-CLISP
           PERFORM 1200-LER-CLIRJ
           PERFORM 1300-LER-CLIMG
           .
       1100-LER-CLISP.
           READ CLISP INTO WS-REG-CLISP
           IF FS-CLISP EQUAL "00"
              COMPUTE WS-CTLIDO-SP = WS-CTLIDO-SP + 1
           ELSE
              IF FS-CLISP EQUAL "10"
                 MOVE HIGH-VALUES          TO WS-CPFCLI-SP
              ELSE
                 MOVE "ERRO NA LEITURA!"   TO WS-MSG
                 MOVE FS-CLISP             TO WS-FS
                 PERFORM 9999-ERRO
              END-IF
           END-IF
           .
       1200-LER-CLIRJ.
           READ CLIRJ INTO WS-REG-CLIRJ
           IF FS-CLISP EQUAL "00"
              COMPUTE WS-CTLIDO-RJ = WS-CTLIDO-RJ + 1
           ELSE
              IF FS-CLIRJ EQUAL "10"
                 MOVE HIGH-VALUES          TO WS-CPFCLI-RJ
              ELSE
                 MOVE "ERRO NA LEITURA!"   TO WS-MSG
                 MOVE FS-CLIRJ             TO WS-FS
                 PERFORM 9999-ERRO
              END-IF
           END-IF
           .
       1300-LER-CLIMG.
           READ CLIMG INTO WS-REG-CLIMG
           IF FS-CLIMG EQUAL "00"
              COMPUTE WS-CTLIDO-MG = WS-CTLIDO-MG + 1
           ELSE
              IF FS-CLIMG EQUAL "10"
                 MOVE HIGH-VALUES          TO WS-CPFCLI-MG
              ELSE
                 MOVE "ERRO NA LEITURA!"   TO WS-MSG
                 MOVE FS-CLIMG             TO WS-FS
                 PERFORM 9999-ERRO
              END-IF
           END-IF
           .
       2000-PROCESSAR.
           IF WS-CPFCLI-SP < WS-CPFCLI-RJ
           AND WS-CPFCLI-SP < WS-CPFCLI-MG
              MOVE WS-REG-CLISP            TO WS-REG-CLIUNIF
              PERFORM 2100-GRAVAR-CLIUNIF
              PERFORM 1100-LER-CLISP
           ELSE
              IF WS-CPFCLI-RJ < WS-CPFCLI-MG
                 MOVE WS-REG-CLIRJ         TO WS-REG-CLIUNIF
                 PERFORM 2100-GRAVAR-CLIUNIF
                 PERFORM 1200-LER-CLIRJ
              ELSE
                 MOVE WS-REG-CLIMG             TO WS-REG-CLIUNIF
                 PERFORM 2100-GRAVAR-CLIUNIF
                 PERFORM 1300-LER-CLIMG
              END-IF
           END-IF
           .
       2100-GRAVAR-CLIUNIF.
           WRITE REG-CLIUNIF FROM WS-REG-CLIUNIF
           IF FS-CLIUNIF EQUAL "00"
              COMPUTE WS-CTGRAV-UNIF = WS-CTGRAV-UNIF + 1
           ELSE
              MOVE "ERRO NA GRAVACAO!!!"   TO WS-MSG
              MOVE FS-CLIUNIF              TO WS-FS
              PERFORM 9999-ERRO
           END-IF
           .
       3000-TERMINO.
           PERFORM 9000-IMPRIME-DATA.
           CLOSE CLISP
           IF FS-CLISP NOT EQUAL "00"
              MOVE "ERRO NO FECHAMENTO DO ARQUIVO CLISP!!!"
                                           TO WS-MSG
              MOVE FS-CLISP                TO WS-FS
              PERFORM 9999-ERRO
           END-IF
           CLOSE CLIRJ
           IF FS-CLIRJ NOT EQUAL "00"
              MOVE "ERRO NO FECHAMENTO DO ARQUIVO CLIRJ!!!"
                                           TO WS-MSG
              MOVE FS-CLIRJ                TO WS-FS
              PERFORM 9999-ERRO
           END-IF
           CLOSE CLIMG
           IF FS-CLIMG NOT EQUAL "00"
              MOVE "ERRO NO FECHAMENTO DO ARQUIVO CLIMG!!!"
                                           TO WS-MSG
              MOVE FS-CLIMG                TO WS-FS
              PERFORM 9999-ERRO
           END-IF
           CLOSE CLIUNIF
           IF FS-CLIUNIF NOT EQUAL "00"
              MOVE "ERRO NO FECHAMENTO DO ARQUIVO CLIUNIF!!!"
                                           TO WS-MSG
              MOVE FS-CLIUNIF              TO WS-FS
              PERFORM 9999-ERRO
           END-IF
           ACCEPT WS-HORARIO-FINAL FROM TIME
           PERFORM 9000-CALCULA-TEMPO-PROC
           MOVE WS-CTLIDO-SP               TO WS-CTLIDO-SP-F
           MOVE WS-CTLIDO-RJ               TO WS-CTLIDO-RJ-F
           MOVE WS-CTLIDO-MG               TO WS-CTLIDO-MG-F
           MOVE WS-CTGRAV-UNIF             TO WS-CTGRAV-UNIF-F
           DISPLAY "=================================================="
           DISPLAY "TOTAL DE CLIENTES SP............: " WS-CTLIDO-SP-F
           DISPLAY "TOTAL DE CLIENTES RJ............: " WS-CTLIDO-RJ-F
           DISPLAY "TOTAL DE CLIENTES MG............: " WS-CTLIDO-MG-F
           DISPLAY "TOTAL DE CLIENTES UNIFICADOS....: "
                   WS-CTGRAV-UNIF-F
           DISPLAY "TEMPO TOTAL DE PROCESSAMENTO....: "
                   WS-TEMPO-PROCESSAMENTO-F
           DISPLAY "TERMINO NORMAL DE PROCESSAMENTO DO GPGO0204"
           DISPLAY "=================================================="
           .
      *----------------------------------------------------------------*
      *    COPYS DA BOOK
      *----------------------------------------------------------------*
           COPY CALCTEMP
           .
           COPY ROTDATA
           .
           COPY ROTERRO
           .
